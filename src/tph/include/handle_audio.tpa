DEFINE_ACTION_FUNCTION HANDLE_AUDIO
  INT_VAR
    music = 0
    quiet = 0
  STR_VAR
    audio_path = EVAL "%MOD_FOLDER%/audio"
    oggdec_path = EVAL "%audio_path%"
    sox_path = EVAL "%audio_path%"
    output_path = "override"
BEGIN
  /* Early versions of BG:EE do not include bgee.lua and
   * PST:EE does not include monkfist.2da
   */
  ACTION_IF !FILE_EXISTS_IN_GAME bgee.lua AND
            !FILE_EXISTS_IN_GAME monkfist.2da
  BEGIN
    ACTION_MATCH "%WEIDU_OS%" WITH
      win32
      BEGIN
        ACTION_IF FILE_EXISTS "%oggdec_path%/oggdec.exe" BEGIN
          ACTION_IF quiet BEGIN
            OUTER_SPRINT cli_quiet "2>NUL" // old oggdec does not have -Q
          END ELSE OUTER_SPRINT cli_quiet ""
          ACTION_BASH_FOR ~%audio_path%~ ~.+\.ogg$~ BEGIN
            OUTER_SPRINT wavfile ~%audio_path%/%BASH_FOR_RES%.wav~
            COPY ~.../fl#inlined/null.file~ ~%wavfile%~
            AT_NOW ~%oggdec_path%/oggdec.exe "%BASH_FOR_FILESPEC%" %cli_quiet%~
            MOVE ~%wavfile%~ ~%output_path%~
          END
        END ELSE BEGIN
          WARN ~WARNING: audio was not installed because oggdec.exe could not be found in %oggdec_path%~
        END
      END

      osx
      BEGIN
        ACTION_IF FILE_EXISTS ~%sox_path%/sox~ BEGIN
          ACTION_IF quiet BEGIN
            OUTER_SPRINT cli_quiet "-q" // can't actually run mac sox to verify
          END ELSE OUTER_SPRINT cli_quiet ""
          AT_NOW ~chmod +x '%sox_path%/sox'~
          ACTION_BASH_FOR ~%audio_path%~ ~.+\.ogg$~ BEGIN
            OUTER_SPRINT wavfile ~%audio_path%/%BASH_FOR_RES%.wav~
            COPY ~.../fl#inlined/null.file~ ~%wavfile%~
            AT_NOW ~'%sox_path%/sox' %cli_quiet% '%BASH_FOR_FILESPEC%' '%wavfile%'~
            MOVE ~%wavfile%~ ~%output_path%~
          END
        END ELSE BEGIN
          WARN ~WARNING: audio was not installed because sox could not be found in %sox_path%~
        END
      END

      unix
      BEGIN
        OUTER_SET installed = 1
        ACTION_IF quiet BEGIN
          OUTER_SPRINT cli_quiet "2>/dev/null" // because Case_ins breaks -Q
        END ELSE OUTER_SPRINT cli_quiet ""
        ACTION_BASH_FOR ~%audio_path%~ ~.+\.ogg$~ BEGIN
          OUTER_SPRINT wavfile ~%audio_path%/%BASH_FOR_RES%.wav~
          COPY ~.../fl#inlined/null.file~ ~%wavfile%~
          AT_NOW ~oggdec '%BASH_FOR_FILESPEC%' %cli_quiet%~
          ACTION_IF FILE_EXISTS ~%wavfile%~ AND !FILE_SIZE ~%wavfile%~ 0 BEGIN
            MOVE ~%wavfile%~ ~%output_path%~
          END ELSE OUTER_SET installed = 0
        END
        ACTION_IF !installed BEGIN
          WARN "WARNING: audio was not installed because WAV files were not found. Are you sure you have oggdec installed?"
        END
      END
      DEFAULT
    END
  END ELSE BEGIN
    ACTION_IF !music BEGIN
      OUTER_SPRINT ext "wav"
    END ELSE BEGIN
      OUTER_SPRINT ext "acm"
    END
    ACTION_BASH_FOR ~%audio_path%~ ~.+\.ogg$~ BEGIN
      COPY_LARGE ~%BASH_FOR_FILESPEC%~ ~%output_path%/%BASH_FOR_RES%.%ext%~
    END
  END
END
